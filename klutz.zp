(load "compiler/ast")

(import-all "ast")

(define SPECIALS (load "translations.zp"))

(define (find-special sym)
  (get-from SPECIALS (->string sym) sym))

(define (treat-unquote val)
  (++ "~"
    (if (eq? (get-from val [:function :value]) 'unquote-splicing) "@" "")
    (->string (treat-val (get-from val [:arguments 0])))))

(define (treat-qq args)
  "dirty fix for AST imperfections"
  (let ((args (car args)))
    (define (treat-val-or-unquote val)
      (if (and (eq? (val :type) :application)
               (in? [unquote unquote-splicing]
                    (get-from val [:function :value])))
        (treat-unquote val)
        (treat-val val)))

    (++ "`(" (->string (treat-val-or-unquote (args :function))) " "
        (string:join (map ($ (->string (treat-val-or-unquote %))) (args :arguments)) " ")
        ")")))

(define (treat-hash-map val)
  (define (treat-kv kv)
    (++ (->string (treat-val (car kv))) " " (->string (treat-val (cadr kv)))))
  (string:join (map treat-kv (val :value)) " "))

(define (treat-coll val)
  (string:join (map ($ (->string (treat-val %))) (val :value)) " "))

(define (translate-colons val)
  "translate the colons to dots and the last one to a slash"
  (regex:gsub r/\.(?!.*\.)/ "/" (regex:gsub r/:/ "." (->string val))))

(define (treat-val val)
  (case (val :type)
    ((:string)
        (++ "\"" (val :value) "\""))
    ((:boolean)
      (->string (val :value)))
    ((:char)
      (let ((quote-special
              ($ (case %
                  ((#\backspace)          "backspace")
                  ((#\newline #\return)   "newline")
                  ((#\tab)                "tab")
                  ((#\space)              "space")
                  (else                   (->string %))))))
        (++ "\\" (quote-special (val :value)))))
    ((:list)
      (++ "'(" (treat-coll val) ")"))
    ((:hash-map)
      (++ "{" (treat-hash-map val) "}"))
    ((:vector)
      (++ "[" (treat-coll val) "]"))
    ((:int)
      (++ "(long " (->string (val :value)) ")"))
    ((:complex)
      (error:from-string "Complex numbers are not supported by this backend"))
    ((:byte-vector)
      (++ "(into-array Byte/TYPE (map byte [" (treat-coll val) "]))"))
    ((:symbol)
      (translate-colons (find-special (val :value))))
    ((:application)
      (if (eq? (get-from val [:function :value]) 'quasiquote)
        (treat-qq (val :arguments))
        `(,(treat-val (get-from val :function)) ,@(map treat-val (val :arguments)))))
    (else (val :value))))

(define (treat-body body)
  (cons (->string (map ($ (% :value)) (body :arguments)))
        (list (treat-val (body :body)))))

(define (write-fn exports fn)
  (let ((name (get-from fn [:function :value]))
        (body (get-from fn [:arguments 0])))
  (write
    (cons
      (if (in? exports (get-from fn [:function :value])) 'defn 'defn-)
      (cons
        name
        (treat-body body))))))

(define (extract-name qq)
  (get-from qq [:arguments 0 :arguments 0 :arguments 0 :value]))

(define (transform modl)
  (let ((name (get-from modl [:arguments 0 :value]))
        (exports (map extract-name
                      (get-from modl [:arguments 1 :arguments]))))
    (write `(ns ,(string->symbol name)))
    (map (curry write-fn exports) (drop 2 (get-from modl :arguments)))))

(|> (car zepto:args)
    parse
    ast:ast
    ($ (get-from % :function))
    transform)
